apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android library for icons packs'
                    url = 'https://github.com/jahirfiquitiva/Blueprint'
                    licenses {
                        license {
                            name = 'License'
                            url = 'https://github.com/jahirfiquitiva/Blueprint/blob/sample/LICENSE.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'jahirfiquitiva'
                            name = 'Jahir Fiquitiva'
                            email = 'hola@jahir.dev'
                        }
                        // Other devs...
                    }
                    scm {
                        connection = 'scm:git:github.com/jahirfiquitiva/Blueprint.git'
                        developerConnection = 'scm:git:ssh://github.com/jahirfiquitiva/Blueprint.git'
                        url = 'https://github.com/jahirfiquitiva/Blueprint/tree/master'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    // Fix Gradle task ordering - ensure build tasks run before publish tasks
    tasks.withType(PublishToMavenRepository) {
        dependsOn tasks.named('bundleReleaseAar')
        dependsOn tasks.named('androidSourcesJar')
        dependsOn tasks.named('sourcesJar')
    }
    tasks.withType(PublishToMavenLocal) {
        dependsOn tasks.named('bundleReleaseAar')
        dependsOn tasks.named('androidSourcesJar')
        dependsOn tasks.named('sourcesJar')
    }
    tasks.withType(GenerateModuleMetadata) {
        dependsOn tasks.named('androidSourcesJar')
        dependsOn tasks.named('sourcesJar')
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            packageGroup = PUBLISH_GROUP_ID
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

// Only enable signing when credentials are available (for Maven Central)
// JitPack doesn't require signing
def canSign = project.hasProperty('signing.keyId') &&
              ext['signing.keyId'] != null &&
              ext['signing.keyId'] != ''

if (canSign) {
    signing {
        sign publishing.publications
    }
}
